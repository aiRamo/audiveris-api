% Feta (not the Font-En-Tja) music font -- small ties
% This file is part of LilyPond, the GNU music typesetter.
%
% Copyright (C) 2011--2022 Bertrand Bordage <bordage.bertrand@gmail.com>
%
% The LilyPond font is free software: you can redistribute it and/or modify
% it under the terms of the GNU General Public License as published by
% the Free Software Foundation, either version 3 of the License, or
% (at your option) any later version, or under the SIL Open Font License.
%
% LilyPond is distributed in the hope that it will be useful,
% but WITHOUT ANY WARRANTY; without even the implied warranty of
% MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
% GNU General Public License for more details.
%
% You should have received a copy of the GNU General Public License
% along with LilyPond.  If not, see <http://www.gnu.org/licenses/>.

fet_begingroup ("ties");

save min_width, max_width, min_height, max_height, thin, thick;

min_width := .5 staff_space#;
max_width := 5 staff_space#;
min_height := .2 staff_space#;
max_height := .4 staff_space#;
thin := 1.2 linethickness;
thick := 2 linethickness;


def draw_lyric_tie (expr width_sharp) =
  save padding, angularity, width, height, depth;

  padding# := .4 staff_space#;
  angularity := 2.5;
  width# := max (min (width_sharp, max_width), min_width);
  height# := (max (width# - min_width, 0) / (max_width - min_width))
             [min_height, max_height];
  depth# := padding# + height#;

  set_char_box (0, 0, depth#, 0);

  define_pixels (width, padding);

  z1 = (-width / 2, -padding);
  z2 = (0, -d);
  z3 = (width / 2, -padding);

  penpos1 (thin, angularity * angle (z2 - z1) + 90);
  penpos2 (thick, 90);
  penpos3 (thin, angularity * angle (z3 - z2) + 90);

  fill z2l
       .. simple_serif (z3l, z3r, 90)
       .. z2r
       .. simple_serif (z1r, z1l, 90)
       .. cycle;

  penlabels (1, 2, 3);
enddef;


fet_beginchar ("short lyric tie", "lyric.short");
  draw_lyric_tie (1.25 staff_space#);
fet_endchar;


fet_beginchar ("default lyric tie", "lyric.default");
  draw_lyric_tie (1.75 staff_space#);
fet_endchar;

fet_endgroup ("ties");
